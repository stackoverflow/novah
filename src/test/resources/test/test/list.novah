module test.list

import novah.stream as Stream
import novah.test

listTests : Unit
listTests =
  test "List stream functions" \_ ->
    let list = [1, 2, 3, 4]
    let stm = Stream.from 1
    let same = List.stream list |> List.fromStream
    List.fromStream stm `shouldBe` [1]
    same `shouldBe` list

  test "List reading functions" \_ ->
    let list = [1, 2, 3, 4]
    list.[2] `shouldBe` 3
    List.nth 2 list `shouldBe` Some 3
    List.nth -1 list `shouldBe` Some 4
    List.first list `shouldBe` Some 1
    List.last list `shouldBe` Some 4
    List.isEmpty list `shouldBe` false
    List.isNotEmpty list `shouldBe` true
    List.contains 4 list `shouldBe` true
    List.slice 1 3 list `shouldBe` [2, 3]
    List.take 2 list `shouldBe` [1, 2]
    List.take 4 [1, 2] `shouldBe` [1, 2]
    List.drop 2 list `shouldBe` [3, 4]
    List.drop 6 list `shouldBe` []
    List.takeWhile (_ < 3) list `shouldBe` [1, 2]
    List.dropWhile (_ < 3) list `shouldBe` [3, 4]
    List.tail list `shouldBe` Some [2, 3, 4]
    List.size list `shouldBe` 4
    List.lastIndex list `shouldBe` 3
    List.lastIndex [] `shouldBe` -1
    (list ++ [5, 6]) `shouldBe` [1, 2, 3, 4, 5, 6]

  test "List writing functions" \_ ->
    let list = [1, 2, 3]
    addFirst 4 list `shouldBe` [4, 1, 2, 3]
    addLast 4 list `shouldBe` [1, 2, 3, 4]
    removeFirst list `shouldBe` [2, 3]
    removeLast list `shouldBe` [1, 2]
    setAt 1 5 list `shouldBe` [1, 5, 3]
    updateAt 1 (_ + 1) list `shouldBe` [1, 3, 3]

  test "List traversing functions" \_ ->
    let list = [1, 2, 3]
    List.map (_ + 1) list `shouldBe` [2, 3, 4]
    List.filter Math.isOdd list `shouldBe` [1, 3]
    List.remove Math.isOdd list `shouldBe` [2]
    List.find Math.isOdd list `shouldBe` Some 1
    List.findIndex Math.isEven list `shouldBe` Some 1
    List.sort [4, 1, 3, 5, 2] `shouldBe` [1, 2, 3, 4, 5]
    List.sortBy String.size ["abcde", "def", "v", "tyui"] `shouldBe` ["v", "def", "tyui", "abcde"]
    List.foldl (-) 0 list `shouldBe` -6
    List.foldr (-) 0 list `shouldBe` 2
    List.reduce (+) list `shouldBe` Some 6
    List.flatten [[1, 2], [3, 4], [5, 6]] `shouldBe` [1, 2, 3, 4, 5, 6]
    List.flatMap (\x -> [x, x]) list `shouldBe` [1, 1, 2, 2, 3, 3]
    List.every (_ < 10) list `shouldBe` true
    List.every (_ < 3) list `shouldBe` false
    List.any (_ < 3) list `shouldBe` true
    List.any (_ < 0) list `shouldBe` false
    List.reverse list `shouldBe` [3, 2, 1]
    List.mapIndexed (\idx i -> if idx < 2 then i * 2 else i * 3) list `shouldBe` [2, 4, 9]
    List.mapSome (\x -> if x > 3 then Some x else None) [2, 3, 4, 5] `shouldBe` [4, 5]
    List.filterIndexed (\idx _ -> idx < 2) list `shouldBe` [1, 2]
    List.removeIndexed (\idx _ -> idx < 2) list `shouldBe` [3]
    List.groupBy Math.isEven [1, 2, 3, 4, 5] `shouldBe` Map.new [true <> [2, 4], false <> [1, 3, 5]]
    List.count Math.isOdd [1, 2, 3, 4, 5] `shouldBe` 3
    List.min [6, 2, 5, 9, 0, 4] `shouldBe` Some 0
    List.min ([] : List Int32) `shouldBe` None
    List.minOr 10 [6, 2, 5, 9, 0, 4] `shouldBe` 0
    List.minOr 10 [] `shouldBe` 10
    List.minBy List.size [[1, 2, 3], [5], [6, 7]] `shouldBe` Some [5]
    List.minOf List.size [[1, 2, 3], [5], [6, 7]] `shouldBe` Some 1
    List.max [6, 2, 5, 9, 0, 4] `shouldBe` Some 9
    List.maxOr 10 [6, 2, 5, 9, 0, 4] `shouldBe` 9
    List.maxOr 10 [] `shouldBe` 10
    List.maxBy List.size [[1, 2, 3], [5], [6, 7]] `shouldBe` Some [1, 2, 3]
    List.maxOf List.size [[1, 2, 3], [5], [6, 7]] `shouldBe` Some 3
    List.sum [5, 6, 7, 8] `shouldBe` 26
    List.sumOf List.size [[1, 2, 3], [4, 5], [6, 7]] `shouldBe` 7
    List.product [5, 6, 7, 8] `shouldBe` 1680
    List.productOf List.size [[1, 2, 3], [4, 5], [6, 7]] `shouldBe` 12
    List.partitionStep 2 3 [1, 2, 3, 4, 5, 6, 7] `shouldBe` [[1, 2], [4, 5]]
    List.partitionAllStep 2 3 [1, 2, 3, 4, 5, 6, 7] `shouldBe` [[1, 2], [4, 5], [7]]
    List.partition 2 [1, 2, 3, 4, 5] `shouldBe` [[1, 2], [3, 4]]
    List.partitionAll 2 [1, 2, 3, 4, 5] `shouldBe` [[1, 2], [3, 4], [5]]
    List.repeat 5 "a" `shouldBe` ["a", "a", "a", "a", "a"]
    List.indices [9, 8, 7, 6] `shouldBe` [0, 1, 2, 3]
    List.zip [1, 2, 3, 4, 5] ["a", "b", "c"] `shouldBe` [1 <> "a", 2 <> "b", 3 <> "c"]
    List.zipWith (+) [1, 2, 3, 4, 5] [10, 11, 12] `shouldBe` [11, 13, 15]
    List.zipAll [[1, 2, 3], [4, 5], [6, 7, 8]] `shouldBe` [[1, 4, 6], [2, 5, 7]]
    List.unzip [1 <> "a", 2 <> "b", 3 <> "c"] `shouldBe` (Tuple [1, 2, 3] ["a", "b", "c"])
    List.removeNones [None, Some 1, None, Some 4] `shouldBe` [1, 4]

  test "Linear list functions" \_ ->
    let list = [1, 2, 3]
    let lin =
      List.withLinear list \l ->
        addFirst 0 l
        addLast 4 l
        setAt 2 5 l
    lin `shouldBe` [0, 1, 5, 3, 4]