/**
 * Functions for Java interoperability.
 */
module novah.java

foreign import type java.lang.AssertionError
foreign import type java.lang.Class
foreign import type java.lang.System
foreign import type java.util.Iterator
foreign import type java.util.Comparator
foreign import type java.util.function.Consumer
foreign import type java.util.function.Predicate
foreign import type java.util.function.BiPredicate
foreign import type java.util.function.BiFunction
foreign import type java.util.function.BinaryOperator
foreign import type java.util.function.UnaryOperator
foreign import type java.util.function.ToLongFunction
foreign import type java.util.function.ToIntFunction
foreign import type novah.Core

/**
 * Returns the Java class of this object.
 */
pub
class : a -> Class a
class obj = (obj as Object)#getClass() as Class a

/**
 * Returns the hash code for this value using
 * the native Java `hashCode` method of x.
 */
pub
hashCode : a -> Int32
hashCode x = (x as Object)#hashCode()

/**
 * Returns true if x is equals y using
 * the native Java `equals` method of x.
 */
pub
equals : a -> a -> Boolean
equals x y = (x as Object)#equals(y as Object)

/**
 * Throws an assetion error if the condition is false.
 */
pub
assert : Boolean -> String -> Unit
assert cond msg =
  if cond
    then ()
    else throw AssertionError#new(format "Assert failed: %s" [msg] as Object)

/**
 * Executes the function for each value in this iterator.
 * Runtime: O(n)
 */
pub
forEach : Iterator a -> (a -> Unit) -> Unit
forEach it fun =
  while (it : Iterator a)#hasNext() do
    fun it#next()

/**
 * Terminates the currently running JVM with the given status.
 * A status different from 0 indicates abnormal termination.
 */
pub
exit : Int32 -> Unit
exit status = System#exit(status)

/**
 * Returns a `java.util.Comparator` based on this ordering function.
 */
pub
toComparator : (a -> a -> Ordering) -> Comparator a
toComparator cmp =
  let comp x y =
    case cmp x y of
      LT -> -1
      EQ -> 0
      GT -> 1
  Core#makeComparator(comp)

/**
 * Creates a `java.util.function.Consumer` out of this function, ignoring the result.
 */
pub
consumer : (a -> b) -> Consumer a
consumer f = Core#makeConsumer(f)

/**
 * Creates a `java.util.function.Predicate` out of this function.
 */
pub
predicate : (a -> Boolean) -> Predicate a
predicate f = Core#makePredicate(f)

/**
 * Creates a `java.util.function.BiPredicate` out of this function.
 */
pub
bipredicate : (a -> b -> Boolean) -> BiPredicate a b
bipredicate f = Core#makeBiPredicate(f)

/**
 * Creates a `java.util.function.ToLongFunction` out of this function.
 */
pub
toLong : (a -> Int64) -> ToLongFunction a
toLong f = Core#makeToLongFunction(f)

/**
 * Creates a `java.util.function.ToIntFunction` out of this function.
 */
pub
toInt : (a -> Int32) -> ToIntFunction a
toInt f = Core#makeToIntFunction(f)

/**
 * Creates a `java.util.function.BinaryOperator` out of this function.
 */
pub
binaryOperator : (a -> a -> a) -> BinaryOperator a
binaryOperator f = Core#makeBinaryOperator(f)

/**
 * Creates a `java.util.function.UnaryOperator` out of this function.
 */
pub
unaryOperator : (a -> a) -> UnaryOperator a
unaryOperator f = Core#makeUnaryOperator(f)

/**
 * Creates a `java.util.function.BiFunction` out of this function.
 */
pub
biFunction : (a -> b -> c) -> BiFunction a b c
biFunction f = Core#makeBiFunction(f)