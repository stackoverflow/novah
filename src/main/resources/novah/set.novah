/**
 * Functions to work with persistent sets.
 * Literal sets can be created using `#{}`.
 */
module novah.set

foreign import java.util.stream.Stream
foreign import io.lacuna.bifurcan.ISet
foreign import io.lacuna.bifurcan.Map
foreign import io.lacuna.bifurcan.Maps
foreign import novah.Core

import novah.stream as S
import novah.java as J
import novah.option as Option
import novah.list as List

/**
 * Creates a new set which uses the element equality function and the provided hash function, or the default object hash function.
 * This function should always be used to create new sets for types that have equality implementations that differ from its own
 * `equals` function.
 * Note that Novah creates default, sensible `equals` and `hashCode` implementations for every type, so this function is only needed
 * for types where the `equals` function and the `Equals` type class implementation diverge.
 */
pub
new : {{ Equals a }} -> Option (a -> Int64) -> Set a
new {{Equals eq}} hashFn =
  let hash = Option.map J.toLong hashFn ?: Maps#-"DEFAULT_HASH_CODE"
  Set#new(hash, J.bipredicate eq.equals)

/**
 * Inserts this element in the set.
 * Runtime: ~O(1)
 */
pub
add : a -> Set a -> Set a
add x (set : Set a) = set#add(x)

/**
 * Removes this element from the set.
 * Runtime: ~O(1)
 */
pub
remove : a -> Set a -> Set a
remove x (set : Set a) = set#remove(x)

/**
 * Returns the size of this set.
 * Runtime: O(1)
 */
pub
size : Set a -> Int64
size (set : Set a) = set#size()

/**
 * Returns true if this set is empty.
 */
pub
isEmpty : Set a -> Boolean
isEmpty (set : Set a) = set#size() == 0L

/**
 * Returns true if this set is not empty.
 * Runtime: O(1)
 */
pub
isNotEmpty : Set a -> Boolean
isNotEmpty (set : Set a) = set#size() != 0L

/**
 * Returns some value at index for this set if it exists or none.
 * Runtime: ~O(1)
 */
pub
nth : Int64 -> Set a -> Option a
nth index (set : Set a) =
  let len = set#size()
  if index < 0L || index >= len
    then None
    else Some set#nth(index)

/**
 * Returns the value at index for this set.
 * *Unsafe*: Will thrown a `java.lang.IndexOutOfBoundsException` if the index doesn't exist.
 * Runtime: ~O(1)
 */
pub
unsafeNth : Int64 -> Set a -> a
unsafeNth index (set : Set a) = set#nth(index)

/**
 * Returns the union of these 2 sets.
 * This is the same as `set1 ++ set2`.
 */
pub
union : Set a -> Set a -> Set a
union (s1 : Set a) s2 = s1#union(s2 as ISet a)

/**
 * Returns all elements in set 1 that are not in set 2.
 */
pub
difference : Set a -> Set a -> Set a
difference (s1 : Set a) s2 = s1#difference(s2 as ISet a)

/**
 * Returns the intersection between these 2 sets.
 */
pub
intersection : Set a -> Set a -> Set a
intersection (s1 : Set a) s2 = s1#intersection(s2 as ISet a)

/**
 * Returns a stream for this set.
 */
pub
stream : Set a -> Stream a
stream (set : Set a) = set#stream()

/**
 * Collects this stream into a set.
 * Runtime: O(n)
 */
pub
fromStream : Stream a -> Set a
fromStream st =
  let lset = #{}#linear()
  S.forEach st \x -> add x lset
  lset#forked()

/**
 * Runs function f for every element of this set, ignoring the result.
 * Runtime: O(n)
 */
pub
forEach : Set a -> (a -> b) -> Unit
forEach (set : Set a) f =
  forEachRange 0L set#size() \idx ->
    f set#nth(idx)

/**
 * Maps function f over all elements of this set
 * returning a set of the same size.
 * Runtime: O(n)
 */
pub
map : (a -> b) -> Set a -> Set b
map f (set : Set a) =
  let lset = #{}#linear()
  forEachRange 0L set#size() \idx ->
    add (f set#nth(idx)) lset
  lset#forked()

/**
 * Returns an array of the elements that match the given predicate.
 * Runtime: O(n)
 */
pub
filter : (a -> Boolean) -> Set a -> Set a
filter pred (set : Set a) =
  let lset = #{}#linear()
  forEachRange 0L set#size() \idx ->
    let elem = set#nth(idx)
    when (pred elem) \_ ->
      add elem lset
  lset#forked()

/**
 * Returns the first occurrence of elem in the set for which the predicate returns true.
 * This function uses the set's equality function, set at creation time.
 * Runtime: O(n)
 */
pub
find : (a -> Boolean) -> Set a -> Option a
find pred (set : Set a) =
  let lset = #{}#linear()
  forEachBreak 0L set#size() \idx ->
    let elem = set#nth(idx)
    if pred elem
      then
        add elem lset
        -1
      else 0
  nth 0L lset

/**
 * Returns true if the element is inside the set.
 * This function uses the set's equality function, set at creation time.
 * Runtime: ~O(1)
 */
pub
contains : a -> Set a -> Boolean
contains elem (set : Set a) = set#contains(elem)

/**
 * Reduces this set to a single value according to the reducing function.
 * Goes from the left to the right.
 * Runtime: O(n)
 */
pub
foldl : (b -> a -> b) -> b -> Set a -> b
foldl f init set = Core#foldSet(f, init, set)

/**
 * A specialized version of `foldl` where the first element of the set is used as initial value.
 * Returns none if the set is empty
 * Runtime: O(n)
 */
pub
reduce : (a -> a -> a) -> Set a -> Option a
reduce f set =
  if isEmpty set
    then None
    else
      let first = set#nth(0L)
      let rest = remove first set
      Some (foldl f first rest)

/**
 * Flattens a set of sets into a single set by concatenating them together.
 * Runtime: O(n)
 */
pub
flatten : Set (Set a) -> Set a
flatten sets =
  if isEmpty sets
    then #{}
    else
      let first = sets#nth(0L)
      let ssets = remove first sets
      foldl (\s1 s2 -> s1 ++ s2) first ssets

/**
 * Like map, but the mapping function should return a set.
 * Flattens the result.
 * Runtime: O(n)
 */
pub
flatMap : (a -> Set b) -> Set a -> Set b
flatMap f set = flatten (map f set)

/**
 * Returns true if the predicate is true for every element in this set.
 * Runtime: O(n)
 */
pub
every : (a -> Boolean) -> Set a -> Boolean
every pred set = Core#setEvery(pred, set)

/**
 * Returns true if the predicate is true for any element in this set.
 * Runtime: O(n)
 */
pub
any : (a -> Boolean) -> Set a -> Boolean
any pred set =
  let res = arrayOf [false]
  forEachBreak 0L (size set) \i ->
    if unsafeNth i set |> pred
      then
        aset 0 true res
        -1
      else 0
  aget 0 res

/**
 * Returns true if every element in the list is contained in the set.
 */
pub
containsAll : List a -> Set a -> Boolean
containsAll list set = List.every (\x -> contains x set) list

/**
 * Groups the elements of the set according
 * to the the function.
 * Ex:
 * ```novah
 * groupBy isOdd #{1, 2, 3, 4, 5, 6}
 * // => { true: #{1, 3, 5}, false: #{2, 4, 6} }
 * ```
 */
pub
groupBy : (a -> b) -> Set a -> Map b (Set a)
groupBy f set =
  let m = Map#empty()#linear()
  let grouper elem =
    let key = f elem
    let entry = m#get(key, #{})
    m#put(key, add elem entry)
  
  forEach set grouper
  m#forked()

/**
 * Creates a linear (mutable) version of the given set and applies the function to it.
 * Returns a forked (persistent) version of the returned linear set.
 * This function should be used when lots of expensive mutating operations have to be run on a set
 * and can be seen as a builder for the resulting set.
 * All functions that alter sets like `add`, `remove`, etc.
 * will mutate a linear set.
 * Ex:
 *   withLinear #{1, 2, 3} \set ->
 *     add 4 set
 *     remove 1 set
 *
 *   -> #{2, 3, 4}
 */
pub
withLinear : Set a -> (Set a -> Set a) -> Set a
withLinear (set : Set a) f =
  let lin = f set#linear()
  (lin : Set a)#forked()