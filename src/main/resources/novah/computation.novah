/**
 * This module defines computation expressions
 * for some types like List and Option.
 */
module novah.computation

import novah.linkedList (LinkedList(..))
import novah.linkedList as LinkedList
import novah.set as Set
import novah.list as List

/**
 * The builder for list computation expressions.
 * Allows to use `do.list` syntax for list comprehensions.
 */
pub
list :
  { bind : List a -> (a -> List b) -> List b
  , "return" : a -> List a
  , combine : List a -> List a -> List a
  , zero : List a
  }
list =
  { bind: flip List.flatMap
  , "return": \x -> [x]
  , combine: \(l1 : List a) l2 -> l1 ++ l2
  , zero: []
  }

/**
 * The builder for set computation expressions.
 * Allows to use `do.set` syntax for set comprehensions.
 */
pub
set :
  { bind : Set a -> (a -> Set b) -> Set b
  , "return" : a -> Set a
  , combine : Set a -> Set a -> Set a
  , zero : Set a
  }
set =
  { bind: flip Set.flatMap
  , "return": \x -> #{x}
  , combine: \(s1 : Set a) s2 -> s1 ++ s2
  , zero: #{}
  }

/**
 * The builder for linked list computation expressions.
 * Allows to use `do.linkedList` syntax for linked list comprehensions.
 */
pub
linkedList :
  { bind : LinkedList a -> (a -> LinkedList b) -> LinkedList b
  , "return" : a -> LinkedList a
  , combine : LinkedList a -> LinkedList a -> LinkedList a
  , zero : LinkedList a
  }
linkedList =
  { bind: flip LinkedList.flatMap
  , "return": \x -> Cons x Nil
  , combine: \(l1 : LinkedList a) l2 -> l1 ++ l2
  , zero: Nil
  }

/**
 * The builder for optional computation expressions.
 * Allows to use `do.option` syntax for option workflows.
 */
pub
option :
  { bind : Option a -> (a -> Option b) -> Option b
  , "return" : a -> Option a
  , combine : Option a -> Option a -> Option a
  , zero : Option a
  }
option =
  { bind: optBind
  , "return": Some
  , combine: optCombine
  , zero: None
  }

/**
 * The builder for success/error computation expressions.
 * Allows to use `do.result` syntax for failable workflows.
 */
pub
result :
  { bind : Result a e -> (a -> Result b e) -> Result b e
  , "return" : a -> Result a e
  }
result =
  { bind: resultBind
  , "return": Ok
  }

optBind : Option a -> (a -> Option b) -> Option b
optBind opt f =
  case opt of
    Some x -> f x
    None -> None

optCombine : Option a -> Option a -> Option a
optCombine x y =
  case x of
    Some _ -> x
    None -> y

resultBind : Result a err -> (a -> Result b err) -> Result b err
resultBind res f =
  case res of
    Ok x -> f x
    Err e -> Err e