module novah.map

foreign import type io.lacuna.bifurcan.Map
foreign import type io.lacuna.bifurcan.IMap
foreign import type io.lacuna.bifurcan.IList
foreign import type io.lacuna.bifurcan.IEntry
foreign import type java.util.function.BiPredicate
foreign import type java.util.function.BiFunction
foreign import type java.util.function.ToLongFunction
foreign import type java.util.function.UnaryOperator
foreign import type novah.Core
foreign import new Map(ToLongFunction, BiPredicate) as newMap
foreign import Map.linear() as mapLinear
foreign import Map.forked() as mapForked
foreign import Map.contains(Object) as mapContains
foreign import Map.union(IMap) as mapUnion
foreign import Map.size() as mapSize
foreign import Map.keys() as mapKeys
foreign import Map.remove(Object) as mapRemove
foreign import Map.update(Object, UnaryOperator) as mapUpdate
foreign import Map:empty() as mapEmpty
foreign import Map.iterator() as mapIterator
foreign import Map.mapValues(BiFunction) as mapMapValues
foreign import IMap.put(Object, Object) as mapPut
foreign import IMap.get(Object) as mapGet
foreign import IMap.values() as imapValues
foreign import IEntry.key() as entryKey
foreign import IEntry.value() as entryValue
foreign import List:from(IList) as listFrom
foreign import Core:mapEvery(Function, Map)
foreign import Core:equalsMap(Map, Map, Function, Function)
foreign import get io.lacuna.bifurcan.Maps:DEFAULT_HASH_CODE as defaultHashCode

import novah.java as Java
import novah.option as Option
import novah.list as List

/**
 * Creates a new map with the supplied entries.
 */
pub
new : List (Tuple k v) -> Map k v
new list =
  let lmap = mapLinear (mapEmpty ())
  List.forEach list \(Tuple key val) -> put key val lmap
  mapForked lmap

/**
 * Creates a new map which uses the key equality function and the provided hash function, or the default object hash function.
 * This function should always be used to create new maps for types that have equality implementations that differ from its own
 * `equals` function.
 * Note that Novah creates default, sensible `equals` and `hashCode` implementations for every type.
 */
pub
newEq : {{ Equals k }} -> Option (k -> Int64) -> Map k v
newEq {{Equals eq}} hashFn =
  let hash = Option.map Java.toLong hashFn ?: defaultHashCode
  newMap hash (Java.bipredicate eq.equals)

/**
 * Returns the size of the map.
 * Runtime: O(1)
 */
pub
size : Map k v -> Int64
size m = mapSize m

/**
 * Inserts or replaces a new key/value pair in the map.
 * Runtime: ~O(1)
 */
pub
put : k -> v -> Map k v -> Map k v
put key val map = unsafeCast (mapPut (unsafeCast map) key val)

/**
 * Returns the value under key if it exists.
 * Runtime: ~O(1)
 */
pub
get : k -> Map k v -> Option v
get k map = mapGet (unsafeCast map) k |> Option.fromOptional

/**
 * Applies the function to the value represented by the key, updating it.
 * Runtime: ~O(1)
 */
pub
update : k -> (v -> v) -> Map k v -> Map k v
update key fun map = mapUpdate map key (Java.unaryOperator fun)

/**
 * Removes the key from this map.
 * Runtime: ~O(1)
 */
pub
remove : k -> Map k v -> Map k v
remove key map = mapRemove map key

/**
 * Returns true if this map contains the key.
 * Runtime: ~O(1)
 */
pub
hasKey : k -> Map k v -> Boolean
hasKey key map = mapContains map key

/**
 * Returns the result of merging the two maps.
 * Repeated keys in the second map will overwrite keys from the first.
 * An alias for `++`.
 */
pub
merge : Map k v -> Map k v -> Map k v
merge m1 m2 = mapUnion m1 (unsafeCast m2)

/**
 * Returns a set of all keys in this map.
 * Runtime: O(1)
 */
pub
keys : Map k v -> Set k
keys map = mapKeys map

/**
 * Returns a list of all values in this map.
 */
pub
values : Map k v -> List v
values map = listFrom (imapValues (unsafeCast map))

/**
 * Executes the function for every key/value pair in this map
 * Runtime: O(n)
 */
pub
forEach : Map k v -> (k -> v -> Unit) -> Unit
forEach map f =
  Java.forEach (mapIterator map) \entry -> f (entryKey entry) (entryValue entry)

/**
 * Maps function f over all entries of this map.
 * Runtime: O(n)
 */
pub
mapValues : (k -> v -> v2) -> Map k v -> Map k v2
mapValues f map = mapMapValues map (Java.biFunction f)

/**
 * Returns a map of the elements that match the given predicate.
 * Runtime: O(n)
 */
pub
filter : (k -> v -> Boolean) -> Map k v -> Map k v
filter pred map =
  let lmap = mapLinear map
  forEach lmap \k v ->
    when (not (pred k v)) \_ -> mapRemove lmap k
  mapForked lmap

/**
 * Returns true if the predicate is true for every entry in this map.
 * Runtime: O(n)
 */
pub
every : (k -> v -> Boolean) -> Map k v -> Boolean
every pred map = mapEvery pred map

pub instance
concatMap : Concat (Map k v)
concatMap = Concat { concat: merge }

pub instance
eqMap : {{ Equals k }} -> {{ Equals v }} -> Equals (Map k v)
eqMap {{Equals eqk}} {{Equals eqv}} = Equals { equals: \x y -> equalsMap x y eqk.equals eqv.equals }

/**
 * Creates a linear (mutable) version of the given map and applies the function to it.
 * Returns a forked (persistent) version of the returned linear map.
 * This function should be used when lots of expensive mutating operations have to be run on a map
 * and can be seen as a builder for the resulting map.
 * All functions that alter maps like `put`, `update`, `remove`, etc.
 * will mutate a linear map.
 * Ex:
 *   withLinear (Map.new ["a" <> 1, "b" <> 2, "c" <> 3]) \map ->
 *     put "d" 4 map
 *     remove "b" map
 *
 *   -> {"a" 1, "c" 3, "d" 4}
 */
pub
withLinear : Map k v -> (Map k v -> Map k v) -> Map k v
withLinear map f = mapForked (f (mapLinear map))